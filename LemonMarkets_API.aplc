:class LemonMarkets_API
⍝ implements the LemonMarkets APIs
⍝ Markets: https://data.lemon.markets/v1/docs#/
⍝ Trading: https://paper-trading.lemon.markets/v1/docs#/
⍝ from https://lemon.markets/
⍝ ------------------------------------------------------
⍝ History:
⍝ 2022, May 29th: added Trading endpoint withdrawal & PIN-Handling
⍝ 2022, May 28th: first release - basic stuff working, need to add more tests and expand functionality
⍝


    :field public APIKey←''
    :field public BaseURL←''
    :field public shared DataURL←'https://data.lemon.markets/v1/'
    :field public cfg←''
    :field public Market
    :field public Trading

    ∇ make1
      :Access public
      :Implements constructor
      home←Home
      cfg←(⎕JSON⍠'Dialect' 'JSON5')1⊃⎕NGET home,'LemonMarkets_API.json5'
      :If 0=⎕NC'cfg.APIKey'
      :OrIf 0=≢cfg.APIKey
          cfg.APIKey←2 ⎕NQ'.' 'GetEnvironment' 'LemonMarkets_APIKEY'
      :EndIf
      0 # ⎕SE.SALT.Load'HttpCommand'
      Market←⎕NEW lMarket cfg
      Trading←⎕NEW lTrading cfg
    ∇


    ∇ R←Home
      :If 0=≢R←{6::'' ⋄ ⍎⍵}'#.LemonMarkets_API.SALT_Data.SourceFile'
      :AndIf 0=≢R←{11:'' ⋄ ⍎⍵}'50 ⎕ATX⍎⊃⎕CLASS ⎕THIS'
          'Home directory not found'⎕SIGNAL 6
      :EndIf
      R←1⊃⎕NPARTS R
    ∇


    ∇ test;lma;r1;r2
      :Access public shared
      lma←⎕NEW ⎕THIS
⍝ request = requests.get("https://data.lemon.markets/v1/instruments/?search=search", headers={"Authorization": "Bearer YOUR-API-KEY"})
      r1←lma.Market.instruments'search=isin=US0378331005'   ⍝ get data for apple
      r2←lma.Market.venues''
      r4←lma.Trading.withdrawal 4711
      ∘∘∘
      ⍝ to be continued;)
    ∇

    :class LemonMarket
        :field public URL
        :field public APIKey

        ∇ R←get what_params
          :Access public
          (what params)←2↑(⊆what_params),⊂''
          url←'https://',URL,what,'/',{0=≢⍵:'' ⋄ '?',⍵}params
          R←'GET'SendRequest url
        ∇

        ∇ R←post what_params_body
          :Access public
          (what params body)←3↑(⊆what_params_body),2⍴⊂''
          url←'https://',URL,what,'/',{0=≢⍵:'' ⋄ '?',⍵}params
          R←'POST'SendRequest url body
        ∇

        ∇ R←{cmd}SendRequest url_parms;h
          h←⎕NEW #.HttpCommand
          h.URL←1⊃,⊆url_parms
          b←2⊃2↑(⊆url_parms),⊂''
          :If 0<≢b
              h.Params←b.({⍵(⍎⍵)}¨⎕NL ¯2)
              h.Headers⍪←'Content-Type' 'application/json'
              h.Headers⍪←'accept' 'application/json'
          :EndIf
          h.Headers←1 2⍴'Authorization'('Bearer ',APIKey)
          :If 0=⎕NC'cmd'
              cmd←'GET'
          :EndIf
          h.Command←cmd
          R←h.Run
        ∇
    :endclass

    :class lMarket :LemonMarket
        ∇ make1 cfg
          :Access public
          :Implements constructor
          APIKey←cfg.APIKey
          URL←cfg.((MarketURL{⍺,⍵~¯1↑⍺}'/'),APIVersion,'/')
        ∇
        ∇ R←venues arg
          :Access public
          R←get(1⊃⎕SI)arg
        ∇
        ∇ R←instruments arg
          :Access public
          R←get(1⊃⎕SI)arg
        ∇
        ∇ R←trades arg
          :Access public
          R←get(1⊃⎕SI)arg
        ∇
        ∇ R←quotes arg
          :Access public
          R←get(1⊃⎕SI)arg
        ∇
    :endclass

    :class lTrading :LemonMarket
        ∇ make1 cfg
          :Access public
          :Implements constructor
          APIKey←cfg.APIKey
          URL←cfg.((((paper≡⊂'true')/'paper-'),TradeURL{⍺,⍵~¯1↑⍺}'/'),APIVersion,'/')
        ∇

        ∇ R←account arg
          :Access public
          R←get(1⊃⎕SI)arg
        ∇

        ∇ R←withdrawal a_p_i
          :Access public
        ⍝ amount [pin [idempotency]]
        ⍝ PIN can be passed as 2nd argument or alternatively via Env (or cfg) "LemonMarkets_PIN"
          parms←⎕NS''
          parms.amount←(10000×1⊃,a_p_i)
          :If 1<≢a_p_i
              parms.pin←2⊃a_p_i
              :If 2<≢a_p_i
                  parms.idempotency←(3⊃a_p_i)
              :EndIf
          :ElseIf 0<≢r←2 ⎕NQ'.' 'GetEnvironment' 'LemonMarkets_PIN'
              parms.pin←r
          :EndIf
          :If ' '=⍥⎕DR parms.pin   ⍝ make sure PIN is a number
              parms.pin←⍎'0',parms.pin∩⎕D
          :EndIf
          R←post'account/withdrawals' ''parms
        ∇
    :endclass
:endclass
